class InfrastructureTile {

    field int basePrice;
    field Player owner;

    constructor InfrastructureTile new(int price) {
        let basePrice = price;
        let owner = null;
        return this;
    }

    method void setOwner(Player p) {
        let owner = p;
        return;
    }

    method Player getOwner() {
        return owner;
    }

    method int calculateRent(Board board) {
        var int numInfras;
        var int rentMultiplier;
        var int pips;
        var int rent;
        
        let pips = Game.rollDice(); 
        
        let numInfras = owner.getInfras();
        
        if (numInfras = 1) {
            let rentMultiplier = 4;
        }
        if (numInfras = 2) {
            let rentMultiplier = 10;
        }

        let rent = pips * rentMultiplier;
        
        return rent;
    }

    method void activate(Player p, Board board) {
        var int rentDue;
        var int buy;
        
        if (owner = null) {
            do Sys.wait(50);
            do Output.moveCursor(7, 7);
            do Output.printString("This is a unclaimed");
            do Output.moveCursor(8, 7);
            do Output.printString("infrastructure land");
            do Output.moveCursor(10, 8);
            do Output.printString("Press Y to buy it");
            do Output.moveCursor(11, 7);
            do Output.printString("Press N to skip it");
            while(~(buy = 89) & ~(buy = 121) & ~(buy = 78) & ~(buy = 110)){
                let buy = Keyboard.keyPressed();
            }
            do Sys.wait(25);
            if (buy = 89 | buy = 121){
                do Sys.wait(25);
                do p.buyInfrastructure(this, basePrice, board);
            }
            return;
        } 
        
        if (~(owner = p)) { 
            let rentDue = calculateRent(board); 
            
            do p.pay(rentDue, board);
            do owner.receive(rentDue);
        }
        return; 
    }
}
