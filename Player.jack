class Player {

    static int idCounter;

    field int id;
    field int location;
    field int balance;
    field boolean inJail;
    field int getOutJails;
    field int infras;
    field int trains;
    field int jailTurns;
    field int numHouses;

    constructor Player new() {
        let id = idCounter;
        let idCounter = idCounter + 1;

        let location = 0;
        let inJail = false;
        let balance = 1500;
        
        let getOutJails = 0;
        let infras = 0;
        let trains = 0;
        let jailTurns = 0;
        
        return this;
    }

    function void init() {
        let idCounter = 0;
        return;
    }

    method void move(int steps, int boardSize) {
        do Player.drawClearPlayer(xCoordinate[location], yCoordinate[location]);
        let location = location + steps;
        
        if (~(location < boardSize)) {
            let location = location - (boardSize * (location / boardSize));
            do receive(200); // Collect $200 for passing GO
        }
        do Player.drawPlayer(id, xCoordinate[location], yCoordinate[location]);

        return;
    }

    method void moveTo(int space) {
        let location = space;
        return;
    }

    // 交錢 (Pay)
    method void pay(int amount, Board board) {
        let balance = balance - amount;
        
        // Check for bankruptcy
        if (balance < 0) {
            do board.handleBankruptcy(this);
        }
        return;
    }

    // 賺錢 (Receive)
    method void receive(int amount) {
        let balance = balance + amount;
        return;
    }

    // 買不買得起 (Can Afford)
    method boolean canAfford(int amount) {
        if (~(balance < amount)) {
            return true;
        }
        return false;
    }

    // 買土地 (Buy Regular Property)
    method void buyProperty(PropertyTile p, int price, Board board) {
        if (canAfford(price)) {
            do pay(price, board);
            do p.setOwner(this);
        }
        return;
    }
    
    // 買設施 (Buy Infrastructure/Utility)
    method void buyInfrastructure(InfrastructureTile i, int price, Board board) {
        if (canAfford(price)) {
            do pay(price, board);
            do i.setOwner(this);
            let infras = infras + 1;
        }
        return;
    }
    
    // 買火車 (Buy Train/Railroad)
    method void buyTrain(TrainTile t, int price, Board board) {
        if (canAfford(price)) {
            do pay(price, board);
            do t.setOwner(this);
            let trains = trains + 1;
        }
        return;
    }

    // 坐牢 (Go To Jail) - FIX: Reset jailTurns
    method void goToJail() {
        let inJail = true;
        do Player.drawClearPlayer(xCoordinate[location], yCoordinate[location]);
        let location = 10;
        do Player.drawPlayer(id, 29, 219); //牢房的座標
        let jailTurns = 0;
        return;
    }

    // 被釋放 (Leave Jail) - FIX: Reset jailTurns
    method void leaveJail() {
        let inJail = false;
        let jailTurns = 0;
        return;
    }
    
    // Track turns spent in jail
    method void incrementJailTurns() {
        let jailTurns = jailTurns + 1;
        return;
    }
    
    //抽到get out jail card
    method void addGetOutOfJailCard() {
        let getOutJails = getOutJails + 1;
        return;
    }

    //使用Get out Jail - FIX: Added safety check
    method void useGetOutOfJailCard() {
        if (getOutJails > 0) {
            let getOutJails = getOutJails - 1;
            do leaveJail();
        }
        return;
    }

        // 蓋房子 (Build House)
    method void buildHouse(PropertyTile p, Board board) {

        // Must own the property
        if (~(p.getOwner() = this)) {
            return;
        }

        // Must afford house
        if (~canAfford(p.getHouseCost())) {
                return;
        }

         // Pay and build
        do pay(p.getHouseCost(), board);
        do p.addHouse();   
        let numHouses=numHouses+1;
        return;
    }


    method int getLocation() {
        return location;
    }

    method int getBalance() {
        return balance;
    }

    method boolean isInJail() {
        return inJail;
    }
    
    method int getInfras() {
        return infras;
    }
    
    method int getTrains() {
        return trains;
    }
    
    method int getID() {
        return id;
    }
    
    method int getJailTurns() {
        return jailTurns;
    }

    method int getNumHouses(){
        return numHouses;
    }

    method int getGetOutJails(){
        return getOutJails;
    }

    function void drawClearPlayer(int x, int y){
        do Screen.drawRectangle(x+1, y+1, x+8, y+8);
        return;
    }

    function void drawPlayer(int id, int x, int y){ //畫出玩家位置
        if (id = 1){ //spade
            do Screen.drawRectangle(x+4, y+1, x+5, y+1);
            do Screen.drawRectangle(x+3, y+2, x+6, y+2);
            do Screen.drawRectangle(x+2, y+3, x+7, y+3);
            do Screen.drawRectangle(x+1, y+4, x+8, y+6);
            do Screen.drawRectangle(x+4, y+7, x+5, y+7);
            do Screen.drawRectangle(x+3, y+8, x+6, y+8);
        }

        if (id = 2){ //heart
            do Screen.drawRectangle(x+2, y+2, x+3, y+2);
            do Screen.drawRectangle(x+6, y+2, x+7, y+2);
            do Screen.drawRectangle(x+1, y+3, x+8, y+5);
            do Screen.drawRectangle(x+2, y+6, x+6, y+6);
            do Screen.drawRectangle(x+3, y+7, x+5, y+7);
            do Screen.drawRectangle(x+4, y+8, x+4, y+8);
        }

        if (id = 3){ //diamond
            do Screen.drawRectangle(x+5, y+1, x+5, y+1);
            do Screen.drawRectangle(x+4, y+2, x+6, y+2);
            do Screen.drawRectangle(x+3, y+3, x+7, y+3);
            do Screen.drawRectangle(x+2, y+4, x+8, y+5);
            do Screen.drawRectangle(x+3, y+6, x+7, y+6);
            do Screen.drawRectangle(x+4, y+7, x+6, y+7);
            do Screen.drawRectangle(x+5, y+8, x+5, y+8);
        }

        if (id = 4){ //club
            do Screen.drawRectangle(x+3, y+1, x+6, y+5);
            do Screen.drawRectangle(x+1, y+3, x+2, y+6);
            do Screen.drawRectangle(x+7, y+3, x+8, y+6);
            do Screen.drawRectangle(x+4, y+6, x+5, y+7);
            do Screen.drawRectangle(x+3, y+8, x+6, y+8);
        }

        return;
    }

}



