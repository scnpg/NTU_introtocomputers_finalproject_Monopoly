class Player {

    static int idCounter;

    field int id;
    field int location;
    field int balance;
    field boolean inJail;
    field int getOutJails;
    field int infras;
    field int trains;
    field int jailTurns;
    field int numHouses;
    field boolean isBankrupt;
    static Array xCoordinate;
    static Array yCoordinate;
    static String prtbuy;
    static String prtnotbuy;
    static String prtgo1;
    static String prtgo2;
    static String prtjail1;
    static String prtjail2;
    static String prtjail3;
    static String prtjail4;
    static String prtjail5;
    static String prtjail61;
    static String prtjail62;
    static String bank;

    constructor Player new() {
        
        let id = idCounter;
        let idCounter = idCounter + 1;

        let location = 0;
        let inJail = false;
        let balance = 1500;
        
        let getOutJails = 0;
        let infras = 0;
        let trains = 0;
        let jailTurns = 0;

        let isBankrupt= false;
        
        return this;
    }

    function void init() {
        let xCoordinate = Array.new(40);
        let yCoordinate = Array.new(40);
        let xCoordinate[0] = 226;
        let yCoordinate[0] = 234;
        let xCoordinate[1] = 196;
        let yCoordinate[1] = 234;
        let xCoordinate[2] = 177;
        let yCoordinate[2] = 234;
        let xCoordinate[3] = 158;
        let yCoordinate[3] = 234;
        let xCoordinate[4] = 139;
        let yCoordinate[4] = 234;
        let xCoordinate[5] = 120;
        let yCoordinate[5] = 234;
        let xCoordinate[6] = 101;
        let yCoordinate[6] = 234;
        let xCoordinate[7] = 82;
        let yCoordinate[7] = 234;
        let xCoordinate[8] = 63;
        let yCoordinate[8] = 234;
        let xCoordinate[9] = 44;
        let yCoordinate[9] = 234;
        let xCoordinate[10] = 3;
        let yCoordinate[10] = 234;
        let xCoordinate[11] = 3;
        let yCoordinate[11] = 196;
        let xCoordinate[12] = 3;
        let yCoordinate[12] = 177;
        let xCoordinate[13] = 3;
        let yCoordinate[13] = 158;
        let xCoordinate[14] = 3;
        let yCoordinate[14] = 139;
        let xCoordinate[15] = 3;
        let yCoordinate[15] = 120;
        let xCoordinate[16] = 3;
        let yCoordinate[16] = 101;
        let xCoordinate[17] = 3;
        let yCoordinate[17] = 82;
        let xCoordinate[18] = 3;
        let yCoordinate[18] = 63;
        let xCoordinate[19] = 3;
        let yCoordinate[19] = 44;
        let xCoordinate[20] = 13;
        let yCoordinate[20] = 24;
        let xCoordinate[21] = 44;
        let yCoordinate[21] = 3;
        let xCoordinate[22] = 63;
        let yCoordinate[22] = 3;
        let xCoordinate[23] = 82;
        let yCoordinate[23] = 3;
        let xCoordinate[24] = 101;
        let yCoordinate[24] = 3;
        let xCoordinate[25] = 120;
        let yCoordinate[25] = 3;
        let xCoordinate[26] = 139;
        let yCoordinate[26] = 3;
        let xCoordinate[27] = 158;
        let yCoordinate[27] = 3;
        let xCoordinate[28] = 177;
        let yCoordinate[28] = 3;
        let xCoordinate[29] = 196;
        let yCoordinate[29] = 3;
        let xCoordinate[30] = 225;
        let yCoordinate[30] = 22;
        let xCoordinate[31] = 235;
        let yCoordinate[31] = 44;
        let xCoordinate[32] = 235;
        let yCoordinate[32] = 63;
        let xCoordinate[33] = 235;
        let yCoordinate[33] = 82;
        let xCoordinate[34] = 235;
        let yCoordinate[34] = 101;
        let xCoordinate[35] = 235;
        let yCoordinate[35] = 120;
        let xCoordinate[36] = 235;
        let yCoordinate[36] = 139;
        let xCoordinate[37] = 235;
        let yCoordinate[37] = 158;
        let xCoordinate[38] = 235;
        let yCoordinate[38] = 177;
        let xCoordinate[39] = 235;
        let yCoordinate[39] = 196;
        let idCounter = 0;
        let prtbuy = "Buy successfully!";
        let prtnotbuy = "Not enough money!";
        let prtgo1 = "Passed GO!";
        let prtgo2 = "Gained $200.";
        let prtjail1 = "You're sent to jail";
        let prtjail2 = "You have ";
        let prtjail3 = "OutJailCards";
        let prtjail4 = "You are released!";
        let prtjail5 = "You have to stay";
        let prtjail61 = "here for ";
        let prtjail62 = " rounds";
        let bank="Bankruptcy";
        return;
    }

    method void print(String msg, int row) {
        do Output.moveCursor(7+row, 7);
        do Output.printString(msg);
        do msg.dispose();
        return;
    }

    method void move(int steps, int boardSize) {
        do Sys.wait(25);
        do Player.drawClearPlayer(id, xCoordinate[location], yCoordinate[location]);
        do Sys.wait(25);
        let location = (location + steps);
        if (~(location < boardSize)) {
            let location = (location - boardSize);
            do Output.moveCursor(7, 10);
            do Output.printString(prtgo1);
            do Output.moveCursor(8, 9);
            do Output.printString(prtgo2);
            do receive(200); // Collect $200 for passing GO
            do Sys.wait(500);
        }

        if (location < 0) {
            let location = location + boardSize;
        }

        do Sys.wait(25);

        do Player.drawPlayer(id, xCoordinate[location], yCoordinate[location]);

        return;
    }

    method void moveTo(int space) {
        do Player.drawClearPlayer(id, xCoordinate[location], yCoordinate[location]);
        let location = space;
        do Sys.wait(25);
        do Player.drawPlayer(id, xCoordinate[location], yCoordinate[location]);
        do Sys.wait(25);
        return;
    }

    // 交錢 (Pay)
    method void pay(int amount, Board board) {
        let balance = balance - amount;
        
        // Check for bankruptcy
        if (balance < 0) {
            do board.handleBankruptcy(this);
            do Output.moveCursor(12,7);
            do Output.printString(bank);
            do Sys.wait(1000); //added
        }
        return;
    }

    // 賺錢 (Receive)
    method void receive(int amount) {
        let balance = balance + amount;
        return;
    }

    // 買不買得起 (Can Afford)
    method boolean canAfford(int amount) {
        if (~(balance < amount)) {
            return true;
        }
        return false;
    }

    // 買土地 (Buy Regular Property)
    method void buyProperty(PropertyTile p, int price, Board board) {
        do Sys.wait(50);
        if (canAfford(price)) {
            do Sys.wait(50);
            do pay(price, board);
            do p.setOwner(this);
            do Player.drawOwner(id, xCoordinate[location], yCoordinate[location]);
            do Output.moveCursor(13, 8);
            do Output.printString(prtbuy);
            do Sys.wait(500);
        }
        else{
            do Output.moveCursor(13, 7);
            do Output.printString(prtnotbuy);
            do Sys.wait(500);
        }
        return;
    }
    
    // 買設施 (Buy Infrastructure/Utility)
    method void buyInfrastructure(InfrastructureTile i, int price, Board board) {
        do Sys.wait(50);
        if (canAfford(price)) {
            do Sys.wait(50);
            do pay(price, board);
            do i.setOwner(this);
            do Player.drawOwner(id, xCoordinate[location], yCoordinate[location]);
            let infras = infras + 1;
            do Output.moveCursor(13, 8);
            do Output.printString(prtbuy);
            do Sys.wait(500);
        }

        else{
            do Output.moveCursor(13, 7);
            do Output.printString(prtnotbuy);
            do Sys.wait(500);
        }
        return;
    }
    
    // 買火車 (Buy Train/Railroad)
    method void buyTrain(TrainTile t, int price, Board board) {
        if (canAfford(price)) {
            do pay(price, board);
            do t.setOwner(this);
            do Player.drawOwner(id, xCoordinate[location], yCoordinate[location]);
            let trains = trains + 1;
        }
        return;
    }

    // 坐牢 (Go To Jail) - FIX: Reset jailTurns
    method void goToJail() {
        let inJail = true;
        do Player.drawClearPlayer(id, xCoordinate[location], yCoordinate[location]);
        do Sys.wait(25);
        let location = 10;
        //do Player.drawPlayer(id, 29, 219); //牢房的座標
        let jailTurns = 0;
        do Output.moveCursor(7, 7);
        do Output.printString(prtjail1);
        do Output.moveCursor(8, 10);
        do Output.printString(prtjail2);
        do Output.printInt(getOutJails);
        do Output.moveCursor(9, 9);
        do Output.printString(prtjail3);
        if(getOutJails>0){
            do Output.moveCursor(10, 7);
            do Output.printString(prtjail4);
            do useGetOutOfJailCard();
        }
        else{
            do Output.moveCursor(10, 7);
            do Output.printString(prtjail5);
            do Output.moveCursor(11, 7);
            do Output.printString(prtjail61);
            do Output.printInt((3-jailTurns));
            do Output.printString(prtjail62);
            let jailTurns = jailTurns+1;
        }
        do Sys.wait(1000);
        return;
    }

    // 被釋放 (Leave Jail) - FIX: Reset jailTurns
    method void leaveJail() {
        let inJail = false;
        let jailTurns = 0;
        return;
    }
    
    // Track turns spent in jail
    method void incrementJailTurns() {
        let jailTurns = jailTurns + 1;
        return;
    }
    
    //抽到get out jail card
    method void addGetOutOfJailCard() {
        let getOutJails = getOutJails + 1;
        return;
    }

    //使用Get out Jail - FIX: Added safety check
    method void useGetOutOfJailCard() {
        if (getOutJails > 0) {
            let getOutJails = getOutJails - 1;
            do leaveJail();
        }
        return;
    }

        // 蓋房子 (Build House)
    method void buildHouse(PropertyTile p, Board board) {

        // Must own the property
        if (~(p.getOwner() = this)) {
            return;
        }

        // Must afford house
        if (~canAfford(p.getHouseCost())) {
            do Output.moveCursor(13, 7);
            do Output.printString(prtnotbuy);
            do Sys.wait(500);
            return;
        }

         // Pay and build
        do pay(p.getHouseCost(), board);
        do p.addHouse();
        let numHouses=numHouses+1;
        do Player.drawLevel(numHouses, xCoordinate[location], yCoordinate[location]);
        do Output.moveCursor(13, 8);
        do Output.printString(prtbuy);
        do Sys.wait(500);
        return;
    }

    method void setBankrupt() {
        let isBankrupt = true;
        return;
    }
    method boolean isBankrupt() {
        return isBankrupt;
    }
    
    method int getLocation() {
        return location;
    }

    method int getBalance() {
        return balance;
    }

    method boolean isInJail() {
        return inJail;
    }
    
    method int getInfras() {
        return infras;
    }
    
    method int getTrains() {
        return trains;
    }
    
    method int getID() {
        return id;
    }
    
    method int getJailTurns() {
        return jailTurns;
    }

    method int getNumHouses(){
        return numHouses;
    }

    method int getGetOutJails(){
        return getOutJails;
    }

    method int getxCoordinate(int index){
        return xCoordinate[index];
    }

    method int getyCoordinate(int index){
        return yCoordinate[index];
    }

    function void drawClearPlayer(int id, int x, int y){
        do Screen.setColor(0);
        //清除玩家位置
        let id = id+1;
        if (id = 1){
            do Screen.drawRectangle(x+1, y+1, x+8, y+8);
        }
        if (id = 2){
            do Screen.drawRectangle(x+9, y+1, x+17, y+8);
        }
        if (id = 3){
            do Screen.drawRectangle(x+1, y+9, x+8, y+17);
        }
        if (id = 4){
            do Screen.drawRectangle(x+9, y+9, x+17, y+17);
        }
        return;
    }

    function void drawPlayer(int id, int x, int y){ 
        //畫出玩家位置
        do Screen.setColor(1);
        let id = id+1;
        if (id = 1){ //spade
            do Screen.drawRectangle(x+4, y+1, x+5, y+1);
            do Screen.drawRectangle(x+3, y+2, x+6, y+2);
            do Screen.drawRectangle(x+2, y+3, x+7, y+3);
            do Screen.drawRectangle(x+1, y+4, x+8, y+6);
            do Screen.drawRectangle(x+4, y+7, x+5, y+7);
            do Screen.drawRectangle(x+3, y+8, x+6, y+8);
        }

        if (id = 2){ //heart
            do Screen.drawRectangle(x+2+8, y+2, x+3+8, y+2);
            do Screen.drawRectangle(x+6+8, y+2, x+7+8, y+2);
            do Screen.drawRectangle(x+1+8, y+3, x+8+8, y+5);
            do Screen.drawRectangle(x+2+8, y+6, x+7+8, y+6);
            do Screen.drawRectangle(x+3+8, y+7, x+6+8, y+7);
            do Screen.drawRectangle(x+4+8, y+8, x+5+8, y+8);
        }

        if (id = 3){ //diamond
            do Screen.drawRectangle(x+5, y+1+8, x+5, y+1+8);
            do Screen.drawRectangle(x+4, y+2+8, x+6, y+2+8);
            do Screen.drawRectangle(x+3, y+3+8, x+7, y+3+8);
            do Screen.drawRectangle(x+2, y+4+8, x+8, y+5+8);
            do Screen.drawRectangle(x+3, y+6+8, x+7, y+6+8);
            do Screen.drawRectangle(x+4, y+7+8, x+6, y+7+8);
            do Screen.drawRectangle(x+5, y+8+8, x+5, y+8+8);
        }

        if (id = 4){ //club
            do Screen.drawRectangle(x+3+8, y+1+8, x+6+8, y+5+8);
            do Screen.drawRectangle(x+1+8, y+3+8, x+2+8, y+6+8);
            do Screen.drawRectangle(x+7+8, y+3+8, x+8+8, y+6+8);
            do Screen.drawRectangle(x+4+8, y+6+8, x+5+8, y+7+8);
            do Screen.drawRectangle(x+3+8, y+8+8, x+6+8, y+8+8);
        }

        return;
    }

    function void drawOwner(int id, int x, int y){ //畫出地產擁有者圖示
        let id = id + 1;
        //先處理x,y偏移量
        if (y = 234) { //第一排
            if (x = 120) { //車站(5)
                let y = y - 19;
            }
            else {
                let x = x + 6;
                let y = y - 6;
            }
        }
        else {
            if (x = 3) { //第二排
                if ((y = 158) | (y = 120)){ //電力(13)和車站(15)
                    let x = x + 32;
                }
                else {
                    let x = x + 19;
                    let y = y + 6;
                }
            }
            else {
                if (y = 3) { //第三排
                    if ((x = 120) | (x = 158)){ //車站(25)和水(27)
                        let x = x + 11;
                        let y = y + 32;
                    }   
                    else {
                        let x = x + 7;
                        let y = y + 25;
                    }
                }
                else {
                    if (x = 235) { //第四排
                        if (y = 120){ //車站(35)
                            let x = x - 20;
                        }   
                        else {
                            let x = x - 12;
                            let y = y + 6;
                        }
                    }
                }
            }
        }

        do Screen.setColor(0);
        do Screen.drawRectangle(x+1, y+1, x+5, y+5);
        do Screen.setColor(1);

        if (id = 1){ //spade
            do Screen.drawRectangle(x+3, y+1, x+3, y+5);
            do Screen.drawRectangle(x+2, y+2, x+4, y+2);
            do Screen.drawRectangle(x+1, y+3, x+5, y+4);
        }

        if (id = 2){ //heart
            do Screen.drawRectangle(x+1, y+1, x+2, y+3);
            do Screen.drawRectangle(x+4, y+1, x+5, y+3);
            do Screen.drawRectangle(x+3, y+2, x+3, y+5);
            do Screen.drawRectangle(x+2, y+4, x+4, y+4);
        }

        if (id = 3){ //diamond
            do Screen.drawRectangle(x+2, y+2, x+4, y+4);
            do Screen.drawRectangle(x+3, y+1, x+3, y+5);
            do Screen.drawRectangle(x+1, y+3, x+5, y+3);
        }

        if (id = 4){ //club
            do Screen.drawRectangle(x+2, y+1, x+4, y+2);
            do Screen.drawRectangle(x+1, y+3, x+5, y+4);
            do Screen.drawPixel(x+3, y+5);
        }

        return;
    }

    function void drawLevel(int level, int x, int y){
        //先處理x,y偏移量
        if (y = 234) { //第一排
                let x = x + 12;
                let y = y - 6;
        }
        if (x = 3){ //電力(13)和車站(15)   
            let x = x + 25;
            let y = y + 6;
        }
        if (y = 3) { //第三排
            let x = x + 13;
            let y = y + 25;
        }
        if (x = 235) { //第四排
            let x = x - 6;
            let y = y + 6;
        }

        do Screen.setColor(0);
        do Screen.drawRectangle(x+1, y+1, x+3, y+5);

        do Screen.setColor(1);
        if(level = 1){
            do Game.draw1(x, y);
        }
        if(level = 2){
            do Game.draw2(x, y);
        }
        if(level = 3){
            do Game.draw3(x, y);
        }
        if(level = 4){
            do Game.draw4(x, y);
        }
        if(level = 5){
            do Game.draw5(x, y);
        }
        
        return;
    }
}



